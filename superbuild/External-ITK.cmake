message( "External project - ITK" )

set( ITK_DEPENDENCIES )
set( _vtkoptions )

if( ${USE_SYSTEM_VTK} MATCHES "OFF" )
  set( ITK_DEPENDENCIES VTK ${ITK_DEPENDENCIES} )
else()
  if( ${VTK_MAJOR_VERSION} EQUAL 6 )
    set( _vtkoptions
      -DModule_ITKVtkGlue:BOOL=ON
      -DVTK_DIR:PATH=${VTK_DIR}
    )
  endif()
endif()

if( ${USE_SYSTEM_HDF5} MATCHES "OFF" )
  set( ITK_DEPENDENCIES HDF5 ${ITK_DEPENDENCIES} )
endif()

set( ITK_DOWNLOAD_VERS "4.13" )
set( ITK_DOWNLOAD_PATCH 0 )
set( ITK_DOWNLOAD_SH256 956f3383e93eb8ffcfdfde96fc27a7d38f2e577f0001c4011f4123be6eb65eda)

ExternalProject_Add( ITK
  DEPENDS ${ITK_DEPENDENCIES}
  URL "http://sourceforge.net/projects/itk/files/itk/${ITK_DOWNLOAD_VERS}/InsightToolkit-${ITK_DOWNLOAD_VERS}.${ITK_DOWNLOAD_PATCH}.tar.gz"
  URL_HASH "SHA256=${ITK_DOWNLOAD_SH256}"
  SOURCE_DIR ITK
  BINARY_DIR ITK-build
  UPDATE_COMMAND ""
  PATCH_COMMAND ""
  CMAKE_GENERATOR ${EP_CMAKE_GENERATOR}
  CMAKE_ARGS
    ${ep_common_args}
    -DBUILD_EXAMPLES:BOOL=OFF
    -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
    -DBUILD_TESTING:BOOL=OFF
    -DCMAKE_BUILD_TYPE:STRING=${BUILD_TYPE}
    -DITK_BUILD_DEFAULT_MODULES:BOOL=ON
    -DModule_ITKReview:BOOL=ON
    -DITK_LEGACY_REMOVE:BOOL=ON
    -DCMAKE_INSTALL_PREFIX:PATH=${INSTALL_DEPENDENCIES_DIR}
    -DITK_USE_SYSTEM_HDF5:BOOL=ON
    -DHDF5_DIR:PATH=${HDF5_DIR}
    ${_vtkoptions}
)

set( ITK_DIR ${INSTALL_DEPENDENCIES_DIR}/lib/cmake/ITK-${ITK_DOWNLOAD_VERS}/ )
